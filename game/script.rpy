# Вы можете расположить сценарий своей игры в этом файле.

define k = Character('Коями', color="#c8ffc8")
define k_nvl = Character('Коями', kind=nvl, color="#c8ffc8")
define k_think = Character('Коями', color="#c8ffc8", what_prefix="...", what_suffix="...", what_size=20, what_italic=True, image="koyami")
define k_think_no_side = Character('Коями', color="#c8ffc8", what_prefix="...", what_suffix="...", what_size=20, what_italic=True)

define alan = Character('Алан', color="#ffffff")
define t = Character('Учитель', color="#ff00ff")
define eric = Character('Эрик', color="#ff0000")
define mom = Character('Мама', color="#c8ffc8")
define alan_sister = Character('Девушка', color="#c8ffc8")

transform left_mid:
    xalign 0.25
    yalign 1.0

transform right_mid:
    xalign 0.75
    yalign 1.0


image alan:
    "alan.png"
    zoom 0.5

image alan small:
    "alan.png"
    zoom 0.05

image koyami:
    "koyami.png"
    zoom 0.5

image side koyami:
    LiveCrop((70,20,300,300),"koyami")
    zoom 0.9

image koyami shy:
    "koyami shy.png"
    zoom 0.5

image koyami_flip_big = im.Flip("koyami.png", horizontal=True)

image koyami flip:
    "koyami_flip_big"
    zoom 0.5

image eric:
    Placeholder("boy")
    zoom 0.7
image teacher:
    Placeholder("boy")
    zoom 0.9

image girl:
    Placeholder("girl")
    zoom 0.6

image alan_sister:
    Placeholder("girl")
    zoom 0.7

image mom:
    Placeholder("girl")
    zoom 0.9

label start:

    scene black
    with fade

    "...Иногда настолько тяжело жить и стараться чего-то добиться, что ты просто отчаиваешься и не хочешь идти дальше..."
    "...Но всегда есть лучик света, который не даёт тебе упасть на колени..."

    "У меня тоже есть этот лучик, парень из старшего класса с именем Алан."

    show alan at center
    with fade

    "Он не замечает меня и даже, наверное, не знает... Я же, всё про него знаю..."

    scene bg city violet garden
    with fade

    k_think "Обычный весенний день... Не так холодно, и не так жарко, поэтому я решила надеть рубашку и юбку."
    k_think "Хотя ветер был не особо доброжелательным в этот миг, меня чуть ли не сдувало."

    show koyami at center
    with dissolve

    k "Меня зовут Коями, и я ученица средних классов."

    scene bg city crossroads
    with fade

    k_think  "На данный момент я иду в школу, мне кажется я туда хожу только ради Алана."
    k_think  "Ну и из-за мамы, которая вечно меня донимает оценками..."

    scene bg school garden
    with dissolve

    k_think "...А вот и стены нашей школы, она очень уютна и красива по-своему..."

    show koyami flip at right
    with dissolve

    show alan small with dissolve:
        yalign 0.48
        xalign 0.25

    k_think_no_side "Опять Алан стоит со своими друзьями. Он так легко с ними общается"
    k_think_no_side "Вот бы и мне так дружить с людьми. Стоит ли мне подойти к ним?"

    menu:
        "Подойти":
            call подойти_к_алану

        "Не подходить":
            call не_подходить_к_алану

    scene bg classroom
    with dissolve

    show koyami flip at right
    with dissolve

    k_think_no_side "А вот и наш класс, очень милое место."
    k_think_no_side "Мои одноклассники не замечают меня, да и я с ними не контактирую, просто учусь и всё."
    k_think_no_side "К слову у меня даже подружки нет. Зачем мне конкурентки?"

    scene bg classroom
    with dissolve

    show koyami flip at right
    with fade

    show teacher at left
    with dissolve

    show eric at center
    with dissolve

    t "– Доброе утро дети, сегодня мы проходим голубую лагуну"
    eric "– Опять?! Мы же проходили её на прошлом уроке!"

    k_think_no_side "Эрик, задира нашего класса. Как же он меня бесит. Если бы он умер, было бы всем лучше. Никто о нем и не вспомнит"

    menu:
        "Заткнуть":
            call заткнуть_эрика

        "Сидеть тихо":
            call не_затыкать_эрика

    scene bg city street evening 2
    with fade

    "Через несколько часов"

    show koyami at left
    with dissolve

    k_think_no_side "Школьный день вышел тяжелым. Поскорее бы домой."

    show alan at right
    show alan_sister at right_mid

    k_think_no_side "Очень странно, Алан идет с какой-то девушкой за ручку и смеются..."
    k_think_no_side "А если это его девушка? Что предпринять?"

    menu:
        "Подойти":
            call подойти_к_алану_с_сестрой

        "Не подходить":
            call не_подходить_к_алану_с_сестрой

    scene bg city seacost dark
    with fade

    show koyami at left
    with dissolve

    k """
        Я дошла до дома. В последнее время, так быстро темнеет на улице.

        Кажется, что уже поздний вечер, и думаешь, что сейчас придешь домой, а там будет стоять мама с недовольным лицом. Опускаешь взгляд на часы и понимаешь, что сейчас только обед.

        На улице такой приятный холодок. Хочется задержаться подольше, погулять где-то, помечтать. Нет, в другой раз, сейчас нужно идти домой и делать уроки. Я вошла в дом.
    """

    scene bg koyami home
    with fade

    show koyami flip at right
    show mom at left
    with dissolve

    mom "- ТЫ БУДЕШЬ КУШАТЬ, КОЯМИ?!"

    menu:
        "буду":
            call буду_есть
        "не буду":
            call не_буду_есть

    scene bg koyami room day
    with dissolve

    show koyami at left
    with dissolve

    k """
        Я забрала свою сумку и пошла вверх по лестнице в свою комнату. Открыла дверь!!!

        В моей комнате было все убрано. Мама убралась в моей комнате?
        Очень на неё не похоже. У неё и правда отличное настроение сегодня.
    """

    nvl clear

    scene bg koyami room
    with dissolve

    show koyami at left
    with dissolve

    k_nvl """
        Открыто окно, ветер на улице усилился и прежнее чувство расслабленности ушло.
        На улице кто-то кричал. Дети. Они о чем-то спорили.

        Хочется так же, ни о чем не заботиться и просто с кем-то спорить о такое неважном. О таком ненужном.
        Я закрыла окно и в комнате стало слишком тихо.

        Было слышно тиканье часов. Уже вечер, у меня есть время сделать все уроки....
    """

    return

label подойти_к_алану_с_сестрой:
    k "– П-привет Алан"
    alan "– О, привет..."
    k_think_no_side "Какой же он милый"
    k "— ты меня, наверное, не помнишь… Мы учимся в одном классе"
    alan "— О нет, я знаю тебя, ты Коями, самая умная девушка в нашем классе"
    show koyami shy at left
    k_think_no_side "я не заметила, как покраснела."
    alan "— мы подумали зайти в кафе по дороге, пойдёшь с нами?"
    k_think_no_side "Мне, конечно, хочется пойти и провести время вместе с Аланом"
    k_think_no_side "Но я не хочу, чтоб эта девушка была с нами. Лучше откажусь, в другой раз как-нибудь"
    return

label не_подходить_к_алану_с_сестрой:
    k_think_no_side "Может, лучше проследить за ними? Я могу столько всего узнать об Алане и об этой девушке."
    k_think_no_side "Но они могут заметить меня и тогда Алан примет меня за сумасшедшую."
    k_think_no_side "Ну уж нет, я не могу так глупо его потерять. Пойду домой, там мама уже заждалась меня."
    alan_sister "– Братик, а мы пойдём в парк завтра?"
    k_think_no_side "Это сказала та девушка? Она что, его сестра?.. Ну и хорошо, можно не беспокоиться."
    alan "– Да, конечно."
    return

label заткнуть_эрика:
    k "– Может ты помолчишь, и мы проведём урок?!"
    eric "– А тебе кто слово давал? Где смелости набралась? Расскажешь мне всему, что научилась после уроков?"

    k_think "После уроков, я совсем позабыла об Эрике. И пошла домой с хорошим настроением."

    scene bg school hall
    with dissolve

    k_think "На выходе из школы я увидела Алана. Он издалека смотрел на Эрика и его компашку."

    show alan at right
    with dissolve

    show eric at center
    with dissolve

    show girl at right_mid
    with dissolve

    k_think "Они доставали девочку. Она не могла ничем ответить. Лишь сидела на полу и смотрела вниз."
    k_think "Но тут Алан резко пошел к Эрику."

    eric "– Смотрите, принц-недотрога идет на веселье."
    alan "– Издеваться над девочкой так приятно, да?"
    eric "– Отойди, по-хорошему, иначе твое личико пострадает."

    k_think "Алан продолжил стоять"
    k_think "Эрик подошел к Алану слишком близко. Он хотел увидеть страх в глазах парня."
    k_think "Алан лишь смотрел на него со строгостью и злобой."
    k_think "Он не колебался. Лишь смотрел на него. В его черные, как душа глаза."

    alan "– Надеюсь, что ты закончил мериться интеллектом и уже уходишь, ведь я вызвал сюда директора."

    k_think "Было видно, что Эрик и его приятели испугались. Они забрали свои вещи и побежали вдоль улицы."
    k_think "Алан подошел к девушке, поднял ее вещи и начал помогать девушке вставать."

    k_think "Я убедилась, что с Аланом все хорошо и пошла домой."

    return

label не_затыкать_эрика:
    k_think "Лучше ничего не буду говорить, кто я такая, чтобы спорить с Эриком?"
    t "– Эрик, мы проходим это по учебному плану! Так что сиди, пожалуйста, смирно и слушай."
    k_think "Он не стал ничего отвечать. Ладно, мне-то какое дело до него… Главное отсидеть уроки и вновь увидеть Алана."
    return

label подойти_к_алану:
    hide alan small
    show alan at left
    with fade

    k "– Привет, Алан, я Коями"
    alan "– Эм… Привет. Откуда ты знаешь моё имя?"
    k "– Я всё про тебя знаю…"
    alan "– Эм… Ты странная…, Пожалуй, я пойду в класс. Скоро урок начнётся."

    hide alan
    with dissolve

    k_think_no_side "Почему он от меня ушёл? Я сказала что-то не так?"
    return

label не_подходить_к_алану:
    k_think_no_side "Лучше не сейчас… В следующий раз, боюсь сказать, что-то не так. Интересно, почему они тут стоят? Ждут кого-то?"
    return

label буду_есть:
    k "– ДА, УЖЕ БЕГУ!"
    k_think_no_side "Мама не спросила где я так долго была. Может, неё есть дела поважнее"
    k_think_no_side "Я сняла свои туфельки и кинула сумку в угол. Прошла по коридору в сторону кухни."
    k_think_no_side "Мама готовит, как настоящая хозяйка. Давно я не видела её такой счастливой."
    k_think_no_side "Приятно, когда близкие люди счастливы. От нее исходит радость и блаженство."
    mom "– У нас сегодня курица с картошкой"
    k "– Здорово, спасибо, мама!"
    k_think_no_side "Мм… Какая вкуснятина, мама редко готовит что-то «большое»."
    k_think_no_side "Обычно, мы завариваем лапшу быстрого приготовления и довольны ей."
    k_think_no_side "Но сейчас, видимо у мамы что-то случилось. Что-то хорошее."
    k "– Спасибо большое, мама. Мне очень приятно, что ты постаралась приготовить ужин для меня, я пойду наверх делать уроки и спать."
    mom "– Сладких снов, доченька"
    k_think_no_side "Я подошла к маме и поцеловала её в щёку. Я почувствовала ее ароматный парфюм. Новый? Мог ли ей кто-то подарить эти духи?"

    return

label не_буду_есть:
    k_think_no_side "Сегодня, наверное, как всегда лапша быстрого приготовления. Нет никакого желания чувствовать ее аромат. Лучше пойду к себе и займусь уроками"
    k "- Спасибо, мам, но я лучше пойду наверх делать уроки"
    k_think_no_side "Очень редко мама расстраивается. Но сейчас я увидела на её лице разочарование, она чуть ли не плакала, я не очень понимала, что произошло."
    k_think_no_side "По-моему, она огорчилась моему ответу. Я сказала что-то не так?"
    "Мама фальшиво улыбнулась и сказала:"
    mom "- Конечно, иди, учись. Сладких снов, Коями."
    k_think_no_side "Чтобы, хоть как-то сгладить ситуация, я подбежала к маме и поцеловала её в щёку."
    k_think_no_side "Очень приятный запах исходил от неё. Купила новый?"
    k "– и тебе, мамочка."

    return
